4. 查询、更新、删除

在之前使用以下命令重建数据库

```
# 注意当前用户的操作权限
mix ecto.drop
mix ecto.create
mix ecto.migrate
```

创建测试数据，进入`iex -S mix`执行

```
people = [
  %Friends.Person{first_name: "Ryan", last_name: "Bigg", age: 28},
  %Friends.Person{first_name: "John", last_name: "Smith", age: 27},
  %Friends.Person{first_name: "Jane", last_name: "Smith", age: 26},
]

Enum.each(people, fn (person) -> Friends.Repo.insert(person) end)
```


## 查询

构建一个`Query`语句

```
Friends.Person |> Ecto.Query.first
```

该语句将生成一个`Ecto.Query`，它是这样的

```
#Ecto.Query<from p in Friends.Person, order_by: [asc: p.id], limit: 1>
```

与`first`相似的还有`last`，它们查询的都是一条记录，可以使用`Friends.Repo.one`进行检索

```
Friends.Person |> Ecto.Query.last |> Friends.Repo.one

#=> %Friends.Person{__meta__: #Ecto.Schema.Metadata<:loaded>, age: 26,
     first_name: "Jane", id: 3, last_name: "Smith"}
```

如果查询的是多条记录，使用`Friends.Repo.one`会引发错误

```
Friends.Person |> Friends.Repo.one

14:47:35.933 [debug] QUERY OK source="people" db=0.0ms idle=9766.0ms
SELECT p0."id", p0."first_name", p0."last_name", p0."age" FROM "people" AS p0 []
** (Ecto.MultipleResultsError) expected at most one result but got 3 in query:

from p0 in Friends.Person

    (ecto 3.4.0) lib/ecto/repo/queryable.ex:115: Ecto.Repo.Queryable.one/3
```

### 提取所有记录

```
Friends.Person |> Friends.Repo.all
```

### 根据id提取一条记录

```
Friends.Person |> Friends.Repo.get(1)
```

### 根据特定属性获取单个记录

```
Friends.Person |> Friends.Repo.get_by(first_name: "Ryan")
```

### 筛选结果

```
Friends.Person |> Ecto.Query.where(last_name: "Smith") |> Friends.Repo.all

# 或者使用

Ecto.Query.from(p in Friends.Person, where: p.last_name == "Smith") |> Friends.Repo.all

```

如果使用变量的话，需要用 `^` 操作符

```
last_name = "Smith"
Friends.Person |> Ecto.Query.where(last_name: ^last_name) |> Friends.Repo.all

# 或者使用

last_name = "Smith"
Ecto.Query.from(p in Friends.Person, where: p.last_name == ^last_name) |> Friends.Repo.all

```

### 撰写查询语句

多个`where`条件可以使用管道操作符进行操作

```
query = Friends.Person |> Ecto.Query.where(last_name: "Smith")
query = query |> Ecto.Query.where(first_name: "Jane")

#Ecto.Query<from p in Friends.Person, where: p.last_name == "Smith",
 where: p.first_name == "Jane">
```

## 更新记录

```
person = Friends.Person |> Ecto.Query.first |> Friends.Repo.one
changeset = Friends.Person.changeset(person, %{age: 29})
Friends.Repo.update(changeset)
```

`update`的操作与`insert`类似


## 删除记录

```
person = Friends.Repo.get(Friends.Person, 1)
Friends.Repo.delete(person)
#=> {:ok,
 %Friends.Person{__meta__: #Ecto.Schema.Metadata<:deleted>, age: 29,
  first_name: "Ryan", id: 2, last_name: "Bigg"}}
```

`delete`操作也返回一个元组，成功元组的第一个元素为`:ok`，失败为`:error`