# 3. 模式匹配

## 匹配操作符`=`

`=` 操作符就是我们的匹配操作符。在 Elixir 当中，用 `=` 来使得左右两边的值相等。如果匹配成功，即左右值相等后，返回这个等式的值。如果两边的值无法匹配，Elixir 则会抛出错误。

```
iex> x = 1
1
iex> 1 = x
1
iex> 2 = x
** (MatchError) no match of right hand side value: 1
```

## pin操作符`^`

当匹配的左边包含变量的时候，匹配操作符同时会做赋值操作。有些时候，这种行为并不是预期的，这种情况下，我们可以使用 ^ 操作符。

使用 pin 操作符，我们就是用已经绑定的值去匹配，而不是重新绑定一个新值

```
iex> x = 1
1
iex> ^x = 2
** (MatchError) no match of right hand side value: 2
iex> {x, ^x} = {2, 1}
{2, 1}
iex> x
2
```

函数子句中应用`pin`操作符的例子：

```
iex> greeting = "Hello"
"Hello"
iex> greet = fn
...>   (^greeting, name) -> "Hi #{name}"
...>   (greeting, name) -> "#{greeting}, #{name}"
...> end
#Function<12.54118792/2 in :erl_eval.expr/5>
iex> greet.("Hello", "Sean")
"Hi Sean"
iex> greet.("Mornin'", "Sean")
"Mornin', Sean"
iex> greeting
"Hello"
```

在上面的例子中，对 greeting 重新赋值只是发生在函数里面，函数体以外，greeting 的值一直是 "Hello"，不会因为函数调用而改变。


## 列表匹配

列表支持在它的首尾进行模式匹配

```
iex> [head | tail] = [1, 2, 3]
[1, 2, 3]
iex> head
1
iex> tail
[2, 3]
```

如果在列表的首部不只匹配一个元素，可以用逗号隔开

```
iex> [a, b, c | _rest] = [1, 2, 3, 4, 5]
[1, 2, 3, 4, 5]
iex> a
1
iex> b
2
iex> c
3
```

`[head | tail]`格式不仅用于模式匹配，还用于将项目预先添加到列表中：

```
iex> list = [1, 2, 3]
[1, 2, 3]
iex> [0 | list]
[0, 1, 2, 3]
```

如果只需要头部，而不要尾部，可以分配给`_`

```
iex> [h|_] = [1,2,3]
[1, 2, 3]
iex> h
1
```

## 字符串匹配

```
iex> "Hello " <> subject = "Hello World!" 
"Hello World!"
iex> subject
"World!"
```

字符串匹配必须在已知大小的开始处匹配，并在末尾捕获未知大小的其余部分。

对于下面这种匹配会报错：

```
iex> greeting <> "Tom" = "Hello Tom"
** (ArgumentError) the left argument of <> operator inside a match should always be a literal binary because its size can't be verified. Got: greeting
    (elixir 1.10.2) lib/kernel.ex:1818: Kernel.invalid_concat_left_argument_error/1
    (elixir 1.10.2) lib/kernel.ex:1790: Kernel.wrap_concatenation/3
    (elixir 1.10.2) lib/kernel.ex:1769: Kernel.extract_concatenations/2
    (elixir 1.10.2) expanding macro: Kernel.<>/2
    iex:6: (file)
```

匹配固定大小的字符串

```
iex> << year::binary-size(4), month::binary-size(2), day::binary-size(2)>> = "20190122" 
"20190122"
iex> year
"2019"
```

